# Copyright 2022 Sutou Kouhei <kou@clear-code.com>
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

@PACKAGE_INIT@

set(datafusion-glib_PACKAGE_PREFIX_DIR "${PACKAGE_PREFIX_DIR}")
set(datafusion-glib_INCLUDE_DIR "${datafusion-glib_PACKAGE_PREFIX_DIR}/@INCLUDE_DIR@")
set(datafusion-glib_LIB_DIR "${datafusion-glib_PACKAGE_PREFIX_DIR}/@LIB_DIR@")
set(datafusion-glib_DEFAULT_LIBRARY "@DEFAULT_LIBRARY@")

include(CMakeFindDependencyMacro)
find_dependency(arrow-glib)
find_dependency(datafusion)

if(datafusion-glib_DEFAULT_LIBRARY STREQUAL "shared" OR
   datafusion-glib_DEFAULT_LIBRARY STREQUAL "both")
  add_library(datafusion-glib::datafusion-glib-shared SHARED IMPORTED)
  set_target_properties(datafusion-glib::datafusion-glib-shared PROPERTIES
    INTERFACE_INCLUDE_DIRECTORIES "${datafusion-glib_INCLUDE_DIR}"
    INTERFACE_LINK_LIBRARIES "datafusion::datafusion;arrow-glib::arrow-glib-shared")
  if(WIN32)
    set_target_properties(datafusion-glib::datafusion-glib-shared PROPERTIES
      IMPORTED_LOCATION "${datafusion-glib_LIB_DIR}/${CMAKE_IMPORT_LIBRARY_PREFIX}datafusion-glib${CMAKE_IMPORT_LIBRARY_SUFFIX}")
  else()
    set_target_properties(datafusion-glib::datafusion-glib-shared PROPERTIES
      IMPORTED_LOCATION "${datafusion-glib_LIB_DIR}/${CMAKE_SHARED_LIBRARY_PREFIX}datafusion-glib${CMAKE_SHARED_LIBRARY_SUFFIX}.@PACKAGE_VERSION@"
      IMPORTED_SONAME "${CMAKE_SHARED_LIBRARY_PREFIX}datafusion-glib.${CMAKE_SHARED_LIBRARY_SUFFIX}.@PACKAGE_VERSION_MAJOR@")
  endif()
endif()

if(datafusion-glib_DEFAULT_LIBRARY STREQUAL "static" OR
   datafusion-glib_DEFAULT_LIBRARY STREQUAL "both")
  add_library(datafusion-glib::datafusion-glib-static STATIC IMPORTED)
  set_target_properties(datafusion-glib::datafusion-glib-static PROPERTIES
    IMPORTED_LOCATION "${datafusion-glib_LIB_DIR}/${CMAKE_STATIC_LIBRARY_PREFIX}datafusion-glib${CMAKE_STATIC_LIBRARY_SUFFIX}"
    INTERFACE_INCLUDE_DIRECTORIES "${datafusion-glib_INCLUDE_DIR}"
    INTERFACE_LINK_LIBRARIES "datafusion::datafusion;arrow-glib::arrow-glib-static")
endif()
